#
# ~/.zsh_prompt
#
# layout:
#
#   ┌─> username @ hostname <─> working dir <─> git prompt
#   └─>
#

if [[ -a /usr/share/git/completion/git-prompt.sh ]]; then
  source /usr/share/git/completion/git-prompt.sh

  export GIT_PS1_SHOWDIRTYSTATE="auto"
  export GIT_PS1_SHOWSTASHSTATE="auto"
  export GIT_PS1_SHOWUNTRACKEDFILES="auto"
  export GIT_PS1_SHOWUPSTREAM="auto"
fi

autoload -U colors && colors

setopt promptsubst
setopt promptpercent

function get_user() {
  if [[ `whoami` == "root" ]]; then
    echo -n "%F{red}%n"
  else
    echo -n "%F{green}%n"
  fi
  echo -n "$reset_color"
}

function get_host() {
  echo -n "%F{cyan}%m$reset_color"
}

function get_pwd() {
  echo -n "%F{yellow}%~$reset_color"
}

function l_arrow() {
  echo -n "%F{blue}<$reset_color"
}

function r_arrow() {
  echo -n "%F{blue}>$reset_color"
}

function separator {
  echo -n "$(l_arrow)%F{cyan}─$(r_arrow)$reset_color"
}

function git_prompt {
  if [[ -d $(git rev-parse --show-toplevel 2>/dev/null) ]]; then
    echo -n "$(separator) $(__git_ps1 "\e[1;35m%s")$reset_color"
  else
    echo -n ""
  fi
}

PROMPT='
%F{cyan}┌─$(r_arrow) $(get_user) %F{blue}@ $(get_host) $(separator) $(get_pwd) $(git_prompt)
%F{cyan}└─$(r_arrow)%f '

